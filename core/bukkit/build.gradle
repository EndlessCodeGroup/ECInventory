plugins {
    id "ru.endlesscode.bukkitgradle" version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

bukkit {
    version = '1.12.2'

    meta {
        authors = ["OsipXD", "EndlessCode Group"]
        main = "ru.endlesscode.rpginventory.RPGInventory"
    }

    run {
        eula = true
        core = "paper"
    }
}

// Replace 'jar' task by 'shadowJar'
tasks.jar.enabled = false
tasks.assemble.dependsOn shadowJar

shadowJar {
    // Understandable filename
    baseName = "$base.name-$project.name"
    classifier = null

    // Avoid conflicts with others
    relocate 'com.typesafe', 'ru.endlesscode.rpginventory.shaded.com.typesafe'
    relocate 'ninja', 'ru.endlesscode.rpginventory.shaded.ninja'
}

dependencies {
    compile(project(":core:api"))
    compileOnly(bukkit())

    // Runtime dependencies will be bundled into the output jar
    runtime("ninja.leaping.configurate:configurate-hocon:3.3") {
        // Guava already in Bukkit
        exclude group: 'com.google.guava'
    }
}
