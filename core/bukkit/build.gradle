plugins {
    id("ru.endlesscode.bukkitgradle") version "0.8.1"
    id("com.github.johnrengelman.shadow") version "2.0.4"
}

bukkit {
    version = "1.13"

    meta {
        name = base.name
        main = "ru.endlesscode.rpginventory.RPGInventoryPlugin"
        authors = ["osipxd", "Dereku", "EndlessCode Group"]
    }

    run {
        eula = true
    }
}

// Replace 'jar' task by 'shadowJar'
tasks.jar.enabled = false
tasks.assemble.dependsOn(shadowJar)

shadowJar {
    // Understandable filename
    baseName = "$base.name-$project.name"
    classifier = null

    // Avoid conflicts with others
    relocate("com.typesafe", "ru.endlesscode.rpginventory.shaded.com.typesafe")
    relocate("ninja", "ru.endlesscode.rpginventory.shaded.ninja")
}

dependencies {
    compile(project(":core:api"))
    compileOnly(bukkit())

    // Runtime dependencies will be bundled into the output jar
    runtime("ninja.leaping.configurate:configurate-hocon:3.3") {
        // Guava already in Bukkit
        exclude(group: "com.google.guava")
    }
}
