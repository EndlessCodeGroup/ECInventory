// Apply shared configuration to subprojects
buildscript {
    ext.kotlinVersion = "1.3.50"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

plugins {
    id("base")
    id("com.github.ben-manes.versions") version "0.27.0"
}

ext.base = project

subprojects {
    apply(plugin: "kotlin")

    description = base.description
    apply(from: base.file("config/config.gradle"))

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        testImplementation("junit:junit:4.12")
        testImplementation("org.jetbrains.kotlin:kotlin-test-junit")
        testImplementation("org.mockito:mockito-inline:2.23.0")
        testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.3"
            languageVersion = "1.3"
        }
    }
}
